apply plugin: 'com.android.application'

// https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def getGitRev = { ->
    def sha = new ByteArrayOutputStream()
    def dirty = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = sha
    }
    exec {
        commandLine 'sh', '-c', 'if test -n "`git status --porcelain --untracked=no`"; then echo "+"; fi'
        standardOutput = dirty
    }
    return sha.toString().trim() + dirty.toString().trim()
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.thomasokken.free42"
        buildConfigField "String", "GitRev", "\"${getGitRev()}\""
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 117
        versionName "2.5.13"
    }
    buildTypes {
        debug {
            ndk {
                abiFilters 'arm64-v8a', 'x86_64'
            }
            manifestPlaceholders = [appLabel: "Free42 Debug"]
            applicationIdSuffix ".debug"
            //signingConfig signingConfigs.release
        }
        release {
            ndk {
                abiFilters 'arm64-v8a'
            }
            manifestPlaceholders = [appLabel: "Free42"]
            minifyEnabled false // R8 doesn't work, app crashes on startup
            signingConfig signingConfigs.release
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
